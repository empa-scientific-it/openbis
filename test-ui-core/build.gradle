evaluationDependsOn(':lib-commonbase')
evaluationDependsOn(':lib-common')
evaluationDependsOn(':api-openbis-java')
evaluationDependsOn(':lib-openbis-common')
evaluationDependsOn(':lib-authentication')
evaluationDependsOn(':lib-dbmigration')
evaluationDependsOn(':server-application-server')
evaluationDependsOn(':server-original-data-store')
evaluationDependsOn(':core-plugin-openbis')

apply from: '../build/javaproject.gradle'

sourceSets {
    main {
        java {
            srcDirs = ['sourceTest/java']
        }
    }
    test {
        java {
            srcDirs = ['source/java']
        }
        resources {
            srcDirs = ['resource']
        }
    }
}

configurations.create('testRuntimeFirst')
configurations.create('testRuntimeSecond')
configurations.create('externalDss')

dependencies {
    api project(path: ':server-original-data-store'),
            project(path: ':server-screening'),
            project(path: ':core-plugin-openbis'),
            'testng:testng:6.8-CISD',
            'selenium:selenium-java:3.141.59'

    testRuntimeFirst 'javax:servlet-api:3.1.0', 'reflections:reflections:0.9.10', 'apache:commons-lang3:3.11'

    testRuntimeSecond 'google:gwt-user:2.4'

    externalDss project(':server-original-data-store')
}

sourceSets.test.runtimeClasspath = configurations.testRuntimeFirst + configurations.testRuntimeSecond + sourceSets.test.runtimeClasspath

task copyWar(type: Copy, dependsOn: project(':core-plugin-openbis').war) {
    from project(':core-plugin-openbis').war
    into 'targets/gradle/openbis-war'
    rename { filename -> 'openbis.war' }
}

import org.gradle.internal.os.OperatingSystem

task makeGeckodriverExecutable(type: Exec) {

    commandLine 'chmod', '+x', 'etc/gecko-driver/linux/geckodriver'
    commandLine 'chmod', '+x', 'etc/gecko-driver/mac/geckodriver'
}

test {
    useTestNG()
    options.suites('source/java/tests.xml')

    jvmArgs '-Xmx2048m', '-Duser.timezone=Europe/Zurich'

    testLogging.showStandardStreams = true
    ignoreFailures = true
    systemProperty 'selenium.dss-runtime-classpath', configurations.externalDss.asPath

    if (OperatingSystem.current().isLinux()) {
        systemProperty 'webdriver.gecko.driver', 'etc/gecko-driver/linux/geckodriver'
    } else if (OperatingSystem.current().isMacOsX()) {
        systemProperty 'webdriver.gecko.driver', 'etc/gecko-driver/mac/geckodriver'
    } else {
        println "Please set up geckodriver"
    }
}

test.dependsOn makeGeckodriverExecutable
test.dependsOn copyWar


